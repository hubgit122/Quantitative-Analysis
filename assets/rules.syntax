// 需要支持min和max函数, 不等号, 逻辑连接词, 数字和四则运算, 和一个持续表达式
S_ruleExpr ::= ruleExpr ("#")?;

ruleExpr ::= ruleTerm | ruleExpr "||" ruleTerm;

ruleTerm ::= ruleFactor | ruleTerm "&&" ruleFactor;

ruleFactor ::= EXPRESSION C EXPRESSION ( "@" (`float` | `digits`) )? | '(' ruleExpr ')'; 

EXPRESSION ::= TERM;
EXPRESSION ::= EXPRESSION '+' TERM;
EXPRESSION ::= EXPRESSION '-' TERM;

TERM ::= FACTOR;
TERM ::= TERM '*' FACTOR;
TERM ::= TERM '/' FACTOR;

FACTOR ::= `float` | `digits`;
FACTOR ::= func '(' args ')' ('.' appendix)? (".." "norest" )?;
FACTOR ::= '-' FACTOR;
FACTOR ::= '(' EXPRESSION ')';

func ::= "max" | "min" | "sum" | "mean" | "average";

args ::= EXPRESSION | args delimiter EXPRESSION ;
delimiter ::= ","|"->";

appendix ::= "highest" | "lowest" | "opening" | "closing" | "quantity";

C ::= "<=" | "==" | ">=" | "<" | ">" ;

ignored ::= `whitespaces` | `cstylecomment`;